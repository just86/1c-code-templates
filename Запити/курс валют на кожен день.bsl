// Крок 1: Отримуємо всі дати з виробничого календаря в заданому періоді
ВЫБРАТЬ
    ДанныеПроизводственногоКалендаря.Дата КАК ДатаМесяц
ПОМЕСТИТЬ ДатаМесяцВТ
ИЗ
    РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
ГДЕ
    ДанныеПроизводственногоКалендаря.Дата МЕЖДУ &ДатаНач И &ДатаКон
;

////////////////////////////////////////////////////////////////////////////////

// Крок 2: Для кожної дати знаходимо максимальну дату курсу (не пізніше поточної дати)
ВЫБРАТЬ
    МАКСИМУМ(КурсыВалют.Период) КАК Период,
    КурсыВалют.Валюта КАК Валюта,
    ДатаМесяцВТ.ДатаМесяц КАК ДатаМесяц
ПОМЕСТИТЬ МаксДатаКурсВТ
ИЗ
    ДатаМесяцВТ КАК ДатаМесяцВТ
        ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
        ПО ДатаМесяцВТ.ДатаМесяц >= КурсыВалют.Период
            И КурсыВалют.Валюта В (&Валюта)
СГРУППИРОВАТЬ ПО
    КурсыВалют.Валюта,
    ДатаМесяцВТ.ДатаМесяц
;

////////////////////////////////////////////////////////////////////////////////

// Крок 3: Отримуємо фактичні значення курсу та кратності
ВЫБРАТЬ
    МаксДатаКурсВТ.Период КАК Период,
    МаксДатаКурсВТ.ДатаМесяц КАК ДатаМесяц,
    МаксДатаКурсВТ.Валюта КАК Валюта,
    КурсыВалют.Курс КАК Курс,
    КурсыВалют.Кратность КАК Кратность
ИЗ
    МаксДатаКурсВТ КАК МаксДатаКурсВТ
        ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
        ПО МаксДатаКурсВТ.Валюта = КурсыВалют.Валюта
            И МаксДатаКурсВТ.Период = КурсыВалют.Период
ГДЕ
   НЕ МаксДатаКурсВТ.Период  ЕСТЬ NULL
УПОРЯДОЧИТЬ ПО
    МаксДатаКурсВТ.ДатаМесяц